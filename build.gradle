plugins {
	id 'eclipse'
	id 'maven-publish'
	id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
	id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
	id 'org.parchmentmc.librarian.forgegradle' version "${parchent_librarian_version}.+"
    id 'com.matthewprenger.cursegradle' version "${cursegradle_version}"
}


//apply plugin: 'net.minecraftforge.gradle'
//apply plugin: 'org.spongepowered.mixin'
//apply plugin: 'org.parchmentmc.librarian.forgegradle'

//apply plugin: 'eclipse'
//apply plugin: 'maven-publish'
//apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

libsDirName = "../output"
distsDirName = "../output"

group = electrodynamics_group
version = electrodynamics_version

base {

	archivesName = electrodynamics_name

}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)



println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')

            arg "-mixin.config=electrodynamics.mixins.json"

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

			property 'mixin.debug.export', 'true'

            mods {
                electrodynamics {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            arg "-mixin.config=electrodynamics.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
			
			property 'mixin.debug.export', 'true'
            
            mods {
                electrodynamics {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            arg "-mixin.config=electrodynamics.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'electrodynamics', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                electrodynamics {
                    source sourceSets.main
                }
            }
        }
    }
}
mixin {
    add sourceSets.main, "electrodynamics.refmap.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    annotationProcessor "org.spongepowered:mixin:${mixin_annotationprocessor_version}:processor"
}

repositories {
	mavenCentral()
	maven {
	    // location of the maven that hosts JEI files since January 2023
	    name = "Jared's maven"
	    url = "https://maven.blamejared.com/"
	}
	maven {
   		// location of a maven mirror for JEI files, as a fallback
    	name = "ModMaven"
    	url = "https://modmaven.dev"
  	}

}

tasks.named('jar', Jar).configure {
	
	manifest {
        attributes([
            "Specification-Title": "electrodynamics",
            "Specification-Vendor": "aurilisdev",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor" :"aurilisdev",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "electrodynamics.mixins.json"
        ])
    }

	finalizedBy 'reobfJar'

}

tasks.register('sourcesJar', Jar).configure {
	dependsOn classes
	from sourceSets.main.allSource
	archiveClassifier = 'sources'
}

tasks.register('deobfJar', Jar).configure {
	from sourceSets.main.output
	archiveClassifier = 'deobf'
}

tasks.named('publish').configure {
	dependsOn 'reobfJar'
}

/*
task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = 'sources'
}
task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}
*/
artifacts {
	archives sourcesJar
	archives deobfJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.curseforge.dependsOn jar

curseforge {
  apiKey = System.getenv("CURSEFORGE_KEY")
  project {
    id = '316867'
    changelog = 'You can find changelog at: https://aurilis.dev/'
    releaseType = 'release'
    addGameVersion "${minecraft_version}"

    mainArtifact(jar) {
      displayName=project.name+" ${electrodynamics_version}"
    }
  }
}
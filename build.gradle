buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = 'CodeChicken'
            url = 'http://chickenbones.net/maven/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'codechicken:CodeChickenCore:1.7.10-1.0.7.46:dev'
        classpath 'codechicken:CodeChickenLib:1.7.10-1.1.3.138:dev'
        classpath 'codechicken:NotEnoughItems:1.7.10-1.0.5.110:dev'
    }
}
apply plugin: 'forge'

sourceSets {
    main {
        java {
            srcDirs = [
                "$projectDir/src/core",
                "$projectDir/src/api",
                "$projectDir/src/externalapi",
                "$projectDir/src/forcefields",
                "$projectDir/src/nuclear",
                "$projectDir/src/missiles"
            ]
            include '**/*.java'
        }

        resources {
            srcDirs = [
                 "$projectDir/src/resources"
            ]
        }
    }
}
// define the properties file
ext.configFile = file "build.properties"
configFile.withReader {
	// read config.
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.version_major}.${config.version_minor}.${config.version_revision}-${config.version_build}"
group= "physica"
archivesBaseName = "PhysicaAll"
sourceCompatibility = 1.8
targetCompatibility = 1.8
libsDirName = "../output"
distsDirName = "../output"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "eclipse"
	
	replace "@VERSION@", project.version
    replaceIn "References.java"
}

repositories {
    maven {
        name = 'CodeChicken'
        url = 'http://chickenbones.net/maven/'
    }
}

dependencies {
    compile 'codechicken:CodeChickenCore:1.7.10-1.0.7.46:dev'
    compile 'codechicken:CodeChickenLib:1.7.10-1.1.3.138:dev'
    compile 'codechicken:NotEnoughItems:1.7.10-1.0.5.110:dev'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task coreJar(type: Jar) {
    baseName = 'PhysicaCore'

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        exclude 'physica/nuclear/**', 'physica/forcefield/**'
    }
}

task nuclearJar(type: Jar) {
    baseName = 'PhysicaNuclearPhysics'

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'physica/nuclear/**'
    }
}

task forcefieldJar(type: Jar) {
    baseName = 'PhysicaForcefield'

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'physica/forcefield/**'
    }
}

task missilesJar(type: Jar) {
    baseName = 'PhysicaMissiles'

    from zipTree(jar.outputs.getFiles().getSingleFile()).matching {
        include 'physica/missiles/**'
    }
}

task incrementBuildNumber() {
    doLast {
        config.version_build = (config.version_build.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
     }
}
coreJar.dependsOn('reobf')
nuclearJar.dependsOn('reobf')
forcefieldJar.dependsOn('reobf')
missilesJar.dependsOn('reobf')
incrementBuildNumber.dependsOn('reobf')
task releaseJars(type: Copy) {
	dependsOn "reobf"
	from incrementBuildNumber
	from coreJar
	from nuclearJar
	from forcefieldJar
	from missilesJar
	destinationDir= new File('/releaseJarsOnly/')
}